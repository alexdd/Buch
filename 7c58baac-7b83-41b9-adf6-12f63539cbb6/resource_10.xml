<?xml version="1.0" encoding="UTF-8"?><topic id="61bcaead-35e5-4f94-b719-e7708391915f"><title id="a1776d29-3de4-4aeb-9f82-1718bfdb333e">Anlegen des Testszenarios auf der ML Konsole</title><body id="daa84b7b-0e02-4448-adfa-1cd9e6b3e289"><p id="2104ae8b-6b06-4e0c-902d-b3dcaca2482b">Die Codefragmente aus dem vorherigen Kapitel sind folgend für eine ML Konsolensitzung aufbereitet:</p><ul id="30e67061-8ce2-46f0-9f22-1a2383c198dbd6e8"><li id="ee196cb3-bf51-41e8-99bb-598d51581f38d6e9"><p id="a626e0cb-bd49-489b-8765-cdb123a70a52d6e10">Anlegen der temporalen Properties: [[code:validStart]], [[code:validEnd]], [[code:systemStart]], [[code:systemEnd]]</p></li><li id="7274f315-72f7-4da1-9f05-e20309215803d6e12"><p id="7690410f-a848-48ac-9ac1-d2b0c3a7cbf5d6e13">Anlegen der Indizes zum Suchen über Zeitbereiche: [[code:database-range-field-index("dateTime", "systemStart",...]]</p></li><li id="e705edf3-6040-481a-ae1c-765a8a7d690cd6e15"><p id="8659af6f-9564-4553-a739-941223db12d0d6e16">Anlegen der zwei Zeitachsen [[code:system]] und [[code:valid]]</p></li><li id="9a60750c-65ee-48ce-9c7c-f2a1a7239f3ad6e18"><p id="fc9beb7e-9542-46c0-ae1b-b797b1b3df50d6e19">Anlegen der temporalen Collection [[code:/perso-verluste]]</p></li><li id="d96c5d9a-ca6f-415c-b4d8-b656361f8e44d6e19"><p id="f7340d19-8dc9-4530-a6b8-40eab7f87459d6e20">Anlegen des Originals am 1.2.2019</p></li><li id="c3fd928f-428c-45b7-bb43-ffc9b7c4ed39d6e22"><p id="ded7a86f-a732-45dd-841d-ad7155ce16dad6e23">Aktualisierung am 6.2.2019</p></li></ul><pre id="b93bbd50-3c01-4edc-a279-6c4324ba70a3d6e25" xml:space="preserve">xquery version "1.0-ml";

import module namespace admin = 
  "http://marklogic.com/xdmp/admin" at "/MarkLogic/admin.xqy";
import module namespace temporal = 
  "http://marklogic.com/xdmp/temporal" at "/MarkLogic/temporal.xqy";

declare namespace local = 'local:';
declare variable $db := "alex-test";

declare function local:create-temporal-fields()
{
  let $config := admin:get-configuration(),
      $dbid := xdmp:database($db)
  return 
    try {
      admin:save-configuration(
        admin:database-add-field($config, $dbid, 
        admin:database-metadata-field("validStart"))),
      admin:save-configuration(
        admin:database-add-field($config, $dbid, 
        admin:database-metadata-field("validEnd"))),
      admin:save-configuration(
        admin:database-add-field($config, $dbid, 
        admin:database-metadata-field("systemStart"))),
      admin:save-configuration(
        admin:database-add-field($config, $dbid, 
        admin:database-metadata-field("systemEnd")))
    } catch ($err) {}
};

declare function local:create-range-index-fields() 
{
  let $config := admin:get-configuration(),
      $dbid := xdmp:database($db)
  return
    try {
      admin:save-configuration(
        admin:database-add-range-field-index($config, $dbid, 
        admin:database-range-field-index("dateTime", "validStart", "", fn:true()))),
      admin:save-configuration(
        admin:database-add-range-field-index($config, $dbid, 
        admin:database-range-field-index("dateTime", "validEnd", "", fn:true()))),
      admin:save-configuration(
        admin:database-add-range-field-index($config, $dbid, 
        admin:database-range-field-index("dateTime", "systemStart", "", fn:true()))),
      admin:save-configuration(
        admin:database-add-range-field-index($config, $dbid, 
        admin:database-range-field-index("dateTime", "systemEnd", "", fn:true())))
    } catch ($err) {}  
};

declare function local:create-axes()
{
  try {
    let $t1 := temporal:axis-create(
          "valid",
          cts:field-reference("validStart", "type=dateTime"),
          cts:field-reference("validEnd", "type=dateTime")),
        $t2 := temporal:axis-create(
          "system",
          cts:field-reference("systemStart", "type=dateTime"),
          cts:field-reference("systemEnd", "type=dateTime"))
     return ()
   } catch ($err) {}
};

declare function local:create-temporal-collection() 
{
  try {
    let $t:= temporal:collection-create("/perso-verluste", "system", "valid")
    return ()
  } catch ($err) {}
};

declare function local:insert-original()
{
  let $root := 
    &lt;vorgang&gt;     
      &lt;perso-id&gt;XYZ&lt;/perso-id&gt;    
      &lt;name&gt;Alex Düsel&lt;/name&gt;   
      &lt;status&gt;gestohlen&lt;/status&gt;
    &lt;/vorgang&gt;,
     $options :=   
    &lt;options xmlns="xdmp:document-insert"&gt;     
      &lt;metadata&gt;        
        &lt;map:map xmlns:map="http://marklogic.com/xdmp/map"&gt;          
          &lt;map:entry key="validStart"&gt;            
	          &lt;map:value&gt;2019-02-01T08:23:11&lt;/map:value&gt;          
	        &lt;/map:entry&gt;          
	        &lt;map:entry key="validEnd"&gt;            
	          &lt;map:value&gt;9999-12-31T11:59:59Z&lt;/map:value&gt;          
	        &lt;/map:entry&gt;         
         &lt;/map:map&gt;    
      &lt;/metadata&gt;  
    &lt;/options&gt;
  return  temporal:document-insert("/perso-verluste", 
                                   "duesel_alex_270774.xml", 
                                   $root, $options)
};

declare function local:insert-update()
{
  let $root :=    
    &lt;vorgang&gt;     
      &lt;perso-id&gt;XYZ&lt;/perso-id&gt;    
      &lt;name&gt;Alex Düsel&lt;/name&gt;   
      &lt;status&gt;gefunden&lt;/status&gt;
    &lt;/vorgang&gt;,
    $options :=   
    &lt;options xmlns="xdmp:document-insert"&gt;     
      &lt;metadata&gt;        
        &lt;map:map xmlns:map="http://marklogic.com/xdmp/map"&gt;          
          &lt;map:entry key="validStart"&gt;            
	          &lt;map:value&gt;2019-02-06T08:00:00&lt;/map:value&gt;          
	        &lt;/map:entry&gt;          
	        &lt;map:entry key="validEnd"&gt;            
	          &lt;map:value&gt;9999-12-31T11:59:59Z&lt;/map:value&gt;          
	        &lt;/map:entry&gt;         
         &lt;/map:map&gt;    
      &lt;/metadata&gt;  
    &lt;/options&gt;
  return  temporal:document-insert("/perso-verluste", 
                                   "duesel_alex_270774.xml", 
                                   $root, $options)
};

( xdmp:invoke-function(local:create-temporal-fields#0),
  xdmp:invoke-function(local:create-range-index-fields#0),
  xdmp:invoke-function(local:create-axes#0),
  xdmp:invoke-function(local:create-temporal-collection#0),
  xdmp:invoke-function(local:insert-original#0),
  xdmp:invoke-function(local:insert-update#0))</pre><note id="5597cd5b-32a6-477d-8659-926309f2daf5d6e27"><p id="5a2c0418-3a17-42dc-a27e-78dda31bc7f6d6e35">Beachtenswert ist hier, dass die einzelnen Schritte als Funktion über [[code:xdmp:invoke-function]] aufgerufen werden. Dieses Konstrukt wird normalerweise benutzt um eine Funktion [[link]]anonym[[fn:https://de.wikipedia.org/wiki/Anonyme_Funktion*Wikipedia Artikel zum Begriff Anonyme Funktion]] zu deklarieren und als Transaktion aufzurufen. MArklogic bietet [[link]]weitere Möglichkeiten[[fn:https://docs.marklogic.com/guide/app-dev/transactions*Arbeiten mit Transaktionen in MarkLogic Server]] transaktional zu arbeiten.</p></note><p id="48ff0142-18b5-4135-986c-b4f3baf3550ed6e37">Lassen wir diese Query auf einer frischen Datenbank laufen, so erhalten wir die folgendes Ergebnis:</p><fig id="fe12f3f3-03bd-4499-a987-303d3a478839d6e41"><desc id="569174d3-830a-41f6-b3bb-d8c760bd3577d6e33">Nach der Ausführung obiger Query gibt es in der DB drei Dokumente, das Orginal, das Split-Dokument und die Aktualisierung. Das Split-Dokuments und das Originals sind als Vorgänger mit Suffix gekennzeichnet.</desc><image id="4fe2001a-090a-4126-b7cf-e6eefb96408fd6e35" href="temporal-documents.png"/></fig></body></topic>